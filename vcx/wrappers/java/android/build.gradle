import java.text.SimpleDateFormat

apply plugin: 'maven'
apply plugin: 'com.android.library'

buildscript {

    repositories {
        jcenter()
        google()
        maven {
            url "https://maven.google.com"
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath 'digital.wup:android-maven-publish:3.3.0'
    }
}

repositories {

    maven { url "https://repo.maven.apache.org/maven2" }
    maven {
        url "https://maven.google.com"
    }
    maven {
        url "https://jitpack.io"
    }
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "./node_modules/react-native/android"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}


group = 'com.evernym.sdk'

def versionMajor() {
    return '1.0.0'
}

//TODO: consider removing buildtime from version number when the wrapper api is on par with vcx-rust api
def buildTime() {
    def df = new SimpleDateFormat("dd-MM-yyyy'T'HH-mm")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def getKrakenPassFromEnv(){
    return System.getenv( 'KRAKEN_PASS' )
}

def getKrakenUserFromEnv(){
    return System.getenv( 'KRAKEN_USER' )
}
version = "${versionMajor()}"

def fullVersionName() {
    return "com.evernym-vcx_${versionMajor()}-${buildTime()}_x86-armv7"
}

description = """vcx"""


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    test.enabled = true
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                srcDirs = ['../src/main/java']
            }
        }
    }

    // IMPORTANT: This is required to fix this issue: https://github.com/android-ndk/ndk/issues/700
    packagingOptions{
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        pickFirst 'lib/arm/libc++_shared.so'
        pickFirst 'lib/armeabi/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
    }

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "${fullVersionName()}"

        testBuildType System.getProperty('testBuildType', 'debug')
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

}

archivesBaseName = fullVersionName()


task uploadToKraken(type:Exec) {
    executable "/bin/sh"
    String krakenPass= getKrakenPassFromEnv()
    String krakenUser= getKrakenUserFromEnv()
    args "-c", "curl --insecure -u $krakenUser:$krakenPass -X POST -F file=@$buildDir/outputs/aar/${archivesBaseName}-release.aar https://kraken.corp.evernym.com/repo/android/upload"
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "skipped", "passed", "failed"
        showStandardStreams true
    }
}

uploadToKraken.dependsOn(assemble)


dependencies {

    implementation('com.github.bright:slf4android:0.1.5'){
        transitive = true
    }
    implementation 'net.java.dev.jna:jna:4.5.0@aar'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version:'3.5'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.6.2'
    implementation group: 'net.sourceforge.streamsupport', name: 'android-retrostreams', version: '1.6.2'
    testImplementation group: 'junit', name: 'junit', version:'4.12'
    testImplementation group: 'commons-io', name: 'commons-io', version:'2.5'
    testImplementation group: 'org.json', name: 'json', version:'20160810'
    testImplementation 'net.java.dev.jna:jna:4.5.0'
    testImplementation 'org.awaitility:awaitility-scala:3.1.2'
    androidTestImplementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    androidTestImplementation('com.facebook.react:react-native:0.59.9') { force = true }
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:26.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    // Set this dependency if you want to use the Hamcrest matcher library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'

}

